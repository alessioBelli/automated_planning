(define (domain emergency_services_logistics) ; Domain name must match problem's

  (:requirements
    :strips
    :negative-preconditions
    :equality
    :typing
    :fluents
    :hierachie
  )
  
  (:types 
    thing - object
    location - object
    depot loc - location
    robot deliverable person box carrier - thing
    food medicine tools - deliverable
    capacity_number - object
  )

  (:predicates
    (at_loc ?t - thing ?l - location)    ;box/person/robot/deliverable is at location l
    (empty ?b - box)         ;box ?b is empty
    (filled ?b - box ?d - deliverable)  ;box ?b is filled with deliverable ?d         Assumption --> maximum one deliverable 
    (loaded ?b - box ?c - carrier)      ;box ?b is loaded on carrier ?c
    (free ?b - box)      ;box ?b is not loaded on a carrier
    (has_content ?p - person ?d - deliverable)   ;person ?p has deliverable ?d
    (capacity ?c - carrier ?n - capacity_number)    ;the current available capacity ?n of the carrier ?c
    (capacity_succ ?n1 - capacity_number ?n2 - capacity_number)     ;capacity number ?n1 is the one after capacity number ?n2
  )

  (:task deliver
		:parameters (?d - deliverable ?p - person)
		:precondition ()
		:effect ()
	)

  (:task pack_load_carrier
		:parameters (?r - robot ?c - carrier ?b - box ?d - deliverable)
		:precondition ()
		:effect ()
	)

  (:task load_carrier
		:parameters (?r - robot ?c - carrier ?b - box)
		:precondition ()
		:effect ()
	)

  (:task unload_carrier_unpack
		:parameters (?r - robot ?c - carrier ?b - box ?d - deliverable)
		:precondition ()
		:effect ()
	)

  (:task unload_carrier
		:parameters (?r - robot ?c - carrier ?b - box)
		:precondition ()
		:effect ()
	)

  (:task fill_box
		:parameters (?d - deliverable ?b - box ?r - robot)
		:precondition ()
		:effect ()
	)

  (:task unpack_box
		:parameters (?d - deliverable ?b - box ?r - robot)
		:precondition ()
		:effect ()
	)

  (:task get_to
		:parameters (?c - carrier ?r - robot ?l - location)
		:precondition ()
		:effect ()
	)

  (:method m_deliver_deliverable
		:parameters (?source - location ?destination - loc ?d - deliverable ?c - carrier ?r - robot ?b - box ?p - person)
		:task (deliver ?d ?p)
		:subtasks (and
		 (task0 (get_to ?c ?r ?source))
		 (task1 (pack_load_carrier ?r ?c ?b ?d))
		 (task2 (get_to ?c ?r ?destination))
     (task3 (unload_carrier_unpack ?r ?c ?b ?d))
		)
		:ordering (and
			(task0 < task1)
			(task1 < task2)
			(task2 < task3)
		)
	)

  (:method m_pack_load_carrier
		:parameters (?r - robot ?c - carrier ?b - box ?d - deliverable)
		:task (pack_load_carrier ?r ?c ?b ?d)
		:subtasks (and
		 (task0 (fill_box ?d ?b ?r))
		 (task1 (load_carrier ?r ?c ?b))
		)
		:ordering (and
			(task0 < task1)
		)
	)

  (:method m_unload_carrier_unpack
		:parameters (?r - robot ?c - carrier ?b - box ?d - deliverable)
		:task (unload_carrier_unpack ?r ?c ?b ?d)
		:subtasks (and
		 (task0 (unload_carrier ?r ?c ?b))
		 (task1 (unpack_box ?d ?b ?r))
		)
		:ordering (and
			(task0 < task1)
		)
	)

  (:method m_load_carrier
		:parameters (?r - robot ?c - carrier ?b - box ?l - location ?n1 - capacity_number ?n2 - capacity_number)
		:task (load_carrier ?r ?c ?b)
		:subtasks (and
		 (task0 (load ?r ?b ?c ?l ?n1 ?n2))
		)
	)

  (:method m_unload_carrier
		:parameters (?r - robot ?c - carrier ?b - box ?l - location ?n1 - capacity_number ?n2 - capacity_number)
		:task (unload_carrier ?r ?c ?b)
		:subtasks (and
		 (task0 (unload ?r ?b ?c ?l ?n1 ?n2))
		)
	)

  (:method m_fill_box
		:parameters (?r - robot ?b - box ?l - location ?d - deliverable)
		:task (fill_box ?d ?b ?r)
		:subtasks (and
		 (task0 (fill ?r ?b ?d ?l))
		)
	)

  (:method m_unpack_box
		:parameters (?r - robot ?b - box ?l - location ?d - deliverable ?p - person)
		:task (unpack_box ?d ?b ?r)
		:subtasks (and
		 (task0 (empty_box ?r ?b ?d ?l))
     (task1 (serve_person ?d ?l ?p))
		)
    :ordering (and
			(task0 < task1)
		)
	)

  (:method m_move_to
		:parameters (?r - robot ?c - carrier ?source - location ?destination - loc)
		:task (get_to ?c ?r ?destination)
		:subtasks (and
		 (task0 (move_carrier ?r ?c ?source ?destination))
		)
	)

  (:method m_back_to_depot
		:parameters (?r - robot ?c - carrier ?source - location ?destination - depot ?n - capacity_number ?n1 - capacity_number)
		:task (get_to ?c ?r ?destination)
		:subtasks (and
		 (task0 (return_to_depot ?r ?c ?source ?destination ?n ?n1))
		)
	)

  (:method m_already_here
		:parameters (?r - robot ?c - carrier ?destination - location)
		:task (get_to ?c ?r ?destination)
		:subtasks (and
		 (task0 (noop ?r ?c ?destination))
		)
	)

  (:action noop
		:parameters (?r - robot ?c - carrier ?l - location)
		:precondition
			(and
				(at_loc ?r ?l)
        (at_loc ?c ?l)
			)
		:effect ()
	)
    ;; fill a box with a content
  (:action fill
    :parameters (?r - robot ?b - box ?d - deliverable ?l - location)
    :precondition (and
      (free ?b)     ;Assumption --> we can fill a box only if it is not loaded on a carrier
      (empty ?b)
      (at_loc ?r ?l)
      (at_loc ?d ?l)
      (at_loc ?b ?l)
    )
    :effect (and
      (not (empty ?b))
      (filled ?b ?d)
      (not (at_loc ?d ?l))
    )
  )
  
  ;; empty a box by leaving the content to the current location
  (:action empty_box
    :parameters (?r - robot ?b - box ?d - deliverable ?l - location)
    :precondition (and
      (not (empty ?b))
      (free ?b)
      (at_loc ?r ?l)          ;Assumption --> same location like fill
      (at_loc ?b ?l)          ;Assumption --> same location like fill
      (filled ?b ?d)
    )
    :effect (and
      (empty ?b)
      (not (filled ?b ?d))
      (at_loc ?d ?l)
    )
  )

;; empty a box by leaving the content to the current location
  (:action serve_person
    :parameters (?d - deliverable ?l - location ?p - person)
    :precondition (and
      (at_loc ?p ?l)
      (at_loc ?d ?l)
      (not (has_content ?p ?d))
    )
    :effect (and
      (has_content ?p ?d)
    )
  )

  ;; the robotic agent can load up to four boxes onto a carrier, which all must be at the same location
  (:action load
    :parameters (?r - robot ?b - box ?c - carrier ?l - location ?n1 - capacity_number ?n2 - capacity_number)
    :precondition (and
      (at_loc ?r ?l)
      (at_loc ?b ?l)
      (at_loc ?c ?l)
      (free ?b)
      (capacity_succ ?n2 ?n1)
      (capacity ?c ?n2)
    )
    :effect (and
      (loaded ?b ?c)
      (not (free ?b))
      (not (at_loc ?b ?l))
      (not (capacity ?c ?n2))
      (capacity ?c ?n1)
    )
  )

  ;; the robotic agent can unload one or more box from the carrier to a location where it is
  (:action unload
    :parameters (?r - robot ?b - box ?c - carrier ?l - location ?n1 - capacity_number ?n2 - capacity_number)
    :precondition (and
      (at_loc ?r ?l)
      (at_loc ?c ?l)
      (loaded ?b ?c)
      (capacity_succ ?n2 ?n1)
      (capacity ?c ?n1)
    )
    :effect (and
      (at_loc ?b ?l)
      (not (loaded ?b ?c))
      (free ?b)
      (not (capacity ?c ?n1))
      (capacity ?c ?n2)
    )
  )
  
  ;; the robotic agent can move the carrier to a location
  (:action move_carrier
    :parameters (?r - robot ?c - carrier ?source - location ?destination - loc)
    :precondition (and
      (not (= ?source ?destination))
      (at_loc ?r ?source)
      (at_loc ?c ?source)
    )
    :effect (and
      (not (at_loc ?r ?source))(not (at_loc ?c ?source))
      (at_loc ?r ?destination)(at_loc ?c ?destination)
    )
  )

  ;; the robotic agent can move the carrier to a location
  (:action return_to_depot
    :parameters (?r - robot ?c - carrier ?source - location ?destination - depot ?n - capacity_number ?n1 - capacity_number)
    :precondition (and
      (not (= ?source ?destination))
      (at_loc ?r ?source)
      (at_loc ?c ?source)
      (capacity ?c ?n)
      (forall (n1)
        (not (capacity_succ ?n1 ?n))
      )
    )
    :effect (and
      (not (at_loc ?r ?source))(not (at_loc ?c ?source))
      (at_loc ?r ?destination)(at_loc ?c ?destination)
    )
  )
)